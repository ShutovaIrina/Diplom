#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Очистить();	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Для Каждого ТекСтрокаВыплата из Выплата Цикл
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Сотрудник = ТекСтрокаВыплата.Сотрудник;
		Движение.Сумма = ТекСтрокаВыплата.Сумма;
	КонецЦикла;
    Движения.Записать();
    
    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
    |	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник.Представление КАК СотрудникПредставление,
    |	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток
    |ИЗ
    |	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&МоментВремени, Сотрудник В
    |		(ВЫБРАТЬ
    |			ВКМ_ВыплатаЗарплатыВыплата.Сотрудник
    |		ИЗ
    |			Документ.ВКМ_ВыплатаЗарплаты.Выплата КАК ВКМ_ВыплатаЗарплатыВыплата
    |		ГДЕ
    |			ВКМ_ВыплатаЗарплатыВыплата.Ссылка = &Ссылка)) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
    |ГДЕ
    |	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток < 0";
    Запрос.УстановитьПараметр("Ссылка", Ссылка );
    ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
    Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
    РезультатЗапроса = Запрос.Выполнить();
   
    Если Не РезультатЗапроса.Пустой() Тогда
        Отказ = Истина;
        ВыборкаОшибки = РезультатЗапроса.Выбрать();
        Пока ВыборкаОшибки.Следующий() Цикл
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = "Сумма выплаты по сотруднику "+ВыборкаОшибки.СотрудникПредставление+" превышает начисление";
            Сообщение.Сообщить();
        КонецЦикла;
    КонецЕсли;
     
КонецПроцедуры	

#КонецОбласти