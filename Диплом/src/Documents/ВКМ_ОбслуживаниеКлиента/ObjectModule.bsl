
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	Если Договор.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание 
		или Дата < Договор.ВКМ_ДатаНачалаДействия
		или Дата > Договор.ВКМ_ДатаОкончанияДействия тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Договор не соответствует условиям");
		Отказ = Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК ЧасыКОплатеКлиенту
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ЧасыКОплатеКлиенту = Запрос.Выполнить().Выгрузить()[0].ЧасыКОплатеКлиенту;
	СтавкаЧасаКлиента = Договор.ВКМ_СтоимостьЧаса;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период,) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|ГДЕ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудник", Специалист);
	Запрос.УстановитьПараметр("Период", Дата);
	
	ПроцентОтРабот = Запрос.Выполнить().Выгрузить()[0].ПроцентОтРабот;
	
	// регистр ВКМ_ВыполненныеКлиентуРаботы
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	
	Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;
	Движение.КоличествоЧасов = ЧасыКОплатеКлиенту;
	Движение.СуммаКОплате = СтавкаЧасаКлиента*ЧасыКОплатеКлиенту;
	
	
	// регистр ВКМ_ВыполненныеСотрудникомРаботы
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Сотрудник = Специалист;
	Движение.ЧасовКОплате = ЧасыКОплатеКлиенту;
	Движение.СуммаКОплате = СтавкаЧасаКлиента*ЧасыКОплатеКлиенту*ПроцентОтРабот/100;
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения) 
	
	Если Не ЭтоНовый() Тогда 
		Запрос = Новый Запрос; 
		Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист,
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
		|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан КАК ВремяНачалаРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан КАК ВремяОкончанияРаботПлан
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка"; 
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка); 
		
		Результат = Запрос.Выполнить(); 
		Выборка = Результат.Выбрать(); 
		
		Если Выборка.Следующий() Тогда 
			ДатаПроведенияРаботБаза = Выборка.ДатаПроведенияРабот;
			СпециалистБаза = Выборка.Специалист;
			ВремяНачалаРаботБаза = Выборка.ВремяНачалаРаботПлан;
			ВремяОкончанияРаботБаза = Выборка.ВремяОкончанияРаботПлан;
		КонецЕсли; 
		
		
		Если ДатаПроведенияРабот <> ДатаПроведенияРаботБаза Или Специалист <> СпециалистБаза
			или ВремяНачалаРаботПлан <> ВремяНачалаРаботБаза или ВремяОкончанияРаботПлан <> ВремяОкончанияРаботБаза Тогда 
			СформироватьСообщение();		
		КонецЕсли;
		
	Иначе 
		СформироватьСообщение();
	Конецесли; 
	
КонецПроцедуры


#КонецОбласти	


#Область СлужебныеПроцедурыИФункции

Процедура СформироватьСообщение() 
	
	Уведомление = Справочники.ВКМ_УведомленияБоту.СоздатьЭлемент();
	Текст = СтрШаблон ("Заказ № %1 для %2. Клиент %3. Дата проведения работ %4, время начала %5, время окончания %6", 
	Номер, Специалист, Клиент, Формат(ДатаПроведенияРабот,"ДФ=dd.MM.yyyy"), Формат(ВремяНачалаРаботПлан,"ДЛФ=T"), 
	Формат(ВремяОкончанияРаботПлан,"ДЛФ=T"));
	Уведомление.ТекстСообщения = Текст;
	Уведомление.Записать();
	
КонецПроцедуры;


#КонецОбласти	